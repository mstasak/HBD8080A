100 REM BLANK SCREEsN
110 P=18
120 OUT P,27; OUT P,46; OUT P,1; OUT P,0
130 GOTO 400

200 REM FILL A RECTANGLE, PIXEL BY PIXEL, USING A COLOR WHICH VARIES WITH X AND Y COORDINATES
210 FOR X=1 TO 1024
220   FOR Y=1 TO 768
230     C = (X + Y) - (X + Y) / 216 * 216
240     GOSUB 1000
250   NEXT Y
260 NEXT X
270 STOP

300 REM DRAW 1000 RANDOM LINES
310 FOR J=1 TO 1000
320   X=RND(1024)-1
330   Y=RND(768)-1
340   V=RND(1024)-1
350   W=RND(768)-1
360   GOSUB 2000
370 NEXT J
380 STOP

400 REM DRAW 100 RANDOM RECTANGLES
410 FOR J=1 TO 100
420   X=RND(1024)-1
430   Y=RND(768)-1
440   W=RND(500)+20
450   H=RND(400)+20
460   GOSUB 3000
470 NEXT J
480 STOP

1000 REM SET A SINGLE PIXEL AT (X, Y) TO COLOR C
1010   OUT P,27; OUT P,46; OUT P,10; OUT P,5
1020   OUT P,X; OUT P,X/256; OUT P,Y; OUT P,Y/256; OUT P,C
1030 RETURN

2000 REM DRAW A LINE FROM (X,Y) TO (V, W)
2001 REM USES CURRENT PEN (DEFAULT 1 PIXEL WIDTH, WHITE)
2010   OUT P,27; OUT P,46; OUT P,20; OUT P,8
2020   OUT P,X; OUT P,X/256; OUT P,Y; OUT P,Y/256
2030   OUT P,V; OUT P,V/256; OUT P,W; OUT P,W/256
2040 RETURN

3000 REM DRAW A RECTANGLE AT (X,Y) SIZED (W, H)
3001 REM USES CURRENT PEN (DEFAULT 1 PIXEL WIDTH, WHITE)
3002   GOSUB 4000; REM SET RANDOM PEN
3004   GOSUB 5000; REM SET RANDOM BRUSH
3010   OUT P,27; OUT P,46; OUT P,32; OUT P,8
3020   OUT P,X; OUT P,X/256; OUT P,Y; OUT P,Y/256
3030   OUT P,W; OUT P,W/256; OUT P,H; OUT P,H/256
3040 RETURN

4000 REM SET RANDOM PEN
4010   OUT P,27; OUT P,46; OUT P,100; OUT P,2
4020   OUT P,RND(216)-1; OUT P,RND(10)-1
4030 RETURN

5000 REM SET RANDOM BRUSH
5010   OUT P,27; OUT P,46; OUT P,110; OUT P,1
5020   OUT P,RND(216)-1 
5030 RETURN
